#!/usr/bin/env perl

use strict;
use warnings;

use Example3;            # Main module to access the version
use Example3::Hello;
use Example3::Bye;
use Example3::HowAreYou;

# VERSION   <- Allows Dist::Zilla to inject the version

package Example3::App;

use Getopt::Long qw(GetOptions);

sub new { bless {}, shift }

sub run {
    my ($self) = @_;

    my %actions = (
        hello => 'Example3::Hello',
        bye   => 'Example3::Bye',
        how   => 'Example3::HowAreYou',
       );

    my ($help, $version);
    GetOptions(
        'h|help'    => \$help,
        'v|version' => \$version,
       );

    if ($version) {
        print "'Example3' version: " . Example3->version() . "\n";
        exit 0;
    }

    if ($help) {
        $self->show_help();
        exit 0;
    }

    # Get action from command-line argument
    my $action = shift @ARGV || 'how';
    my $class  = $actions{$action} // 'Example3::HowAreYou';

    # Run the selected action
    my $obj = $class->new;
    print $obj->message . "!\n";
}

sub show_help {
    print <<"HELP";
Usage: $0 [options] [hello|bye|how]

Options:
  -h, --help      Show this help message
  -v, --version   Display the script version

Commands:
  hello           Say hello
  bye             Say goodbye
  how             Ask how are you (default)
HELP
}

# Run the script
my $app = Example3::App->new();
$app->run();
