#!/usr/bin/env perl

use strict;
use warnings;

use Example4;            # Main module to access the version
use Example4::Hello;
use Example4::Bye;
use Example4::HowAreYou;

# VERSION   <- Allows Dist::Zilla to inject the version

package Example4::App;

use Getopt::Long qw(GetOptions);

sub new { bless {}, shift }

sub run {
    my ($self) = @_;

    my %actions = (
        hello => 'Example4::Hello',
        bye   => 'Example4::Bye',
        how   => 'Example4::HowAreYou',
       );

    my ($help, $version);
    GetOptions(
        'h|help'    => \$help,
        'v|version' => \$version,
       );

    if ($version) {
        print "'Example4' version: " . Example4->version() . "\n";
        exit 0;
    }

    if ($help) {
        $self->show_help();
        exit 0;
    }

    # Get action from command-line argument
    my $action = shift @ARGV || 'how';
    my $class  = $actions{$action} // 'Example4::HowAreYou';

    # Run the selected action
    my $obj = $class->new;
    print $obj->message . "!\n";
}

sub show_help {
    print <<"HELP";
Usage: $0 [options] [hello|bye|how]

Options:
  -h, --help      Show this help message
  -v, --version   Display the script version

Commands:
  hello           Say hello
  bye             Say goodbye
  how             Ask how are you (default)
HELP
}

# Run the script
my $app = Example4::App->new();
$app->run();
